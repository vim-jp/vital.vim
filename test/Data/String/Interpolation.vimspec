Describe Data.String.Interpolation

  Before all
    let g:I = vital#vital#new().import('Data.String.Interpolation')

    function! InterpolationFunc() abort
      let year = 2015
      return g:I.interpolate('Happy new year, ${year}!', l:)
    endfunction

    function! InterpolationFuncA(x, y, ...) abort
      return g:I.interpolate('x is ${x}. y is ${y}', a:)
    endfunction
  End

  After all
    delfunction InterpolationFunc
    delfunction InterpolationFuncA
  End

  Describe .interpolate()
    It evaluate lots of patterns
      let r = InterpolationFunc()
      Assert Equals(r, 'Happy new year, 2015!')
      Assert Equals(g:I.interpolate('It handles Function as well: ${InterpolationFunc()}')
      \  , 'It handles Function as well: Happy new year, 2015!')
      Assert Equals(g:I.interpolate('Hi, my name is ${name}.', {'name': 'haya14busa'})
      \  , 'Hi, my name is haya14busa.')
      Assert Equals(g:I.interpolate('1 + 1 = ${1 + 1}'), '1 + 1 = 2')
      Assert Equals(g:I.interpolate('${repeat} and ${repeat}', {'repeat': 'again'}), 'again and again')
      Assert Equals(g:I.interpolate('${0}'), '0')
      Assert Equals(g:I.interpolate('${string({})}'), '{}')
      Assert Equals(g:I.interpolate('${string({"a": "hello"})}'), "{'a': 'hello'}")
      let x = '1 + 2 + 3 + 4 + 5'
      Assert Equals(g:I.interpolate('${x} = ${eval(x)} ${"${x}"}', l:)
      \  , '1 + 2 + 3 + 4 + 5 = 15 ${x}')
      let y = '${y}'
      Assert Equals(g:I.interpolate('${y}', l:), '${y}')
      Assert Equals(g:I.interpolate('${repeat}${repeat}', {'repeat': 'again'}), 'againagain')
      Assert Equals(g:I.interpolate('${1}${2}${3}'), '123')
      Assert Equals(g:I.interpolate('${x}${y}', {'x': '${y}', 'y': '${x}'}), '${y}${x}')
      Assert Equals(g:I.interpolate('hi${string({"a": {}})}foo'), "hi{'a': {}}foo")
      Assert Equals(g:I.interpolate('hi${string({"a": {"b": {}}})}foo'), "hi{'a': {'b': {}}}foo")
      " line-break for syntax
      Assert Equals(g:I.interpolate('${1 + 1 . "}{''"}'),
      \"2}{'")
      Assert Equals(g:I.interpolate("${'}'}"), '}')
      Assert Equals(g:I.interpolate("${'{'}"), '{')
      Assert Equals(g:I.interpolate("${'{}}}{{}{}'}"), '{}}}{{}{}')
      Assert Equals(g:I.interpolate("${'hoge\"{'}"), 'hoge"{')
    End
    It handle reassignment different type
      Assert Equals(g:I.interpolate('${a} ${b}', {'a': 1, 'b': '1'}), '1 1')
      if !has('patch-8.2.2948')
        Throws /Vim(let):E806:/ g:I.interpolate('${a} ${b}', {'a': 1, 'b': 1.0E-6})
      else
        Assert Equals(g:I.interpolate('${a} ${b}', {'a': 1, 'b': 1.0E-6}), '1 1.0e-6')
      endif
    End
    It doesn't raise Vim(let):E704: Funcref variable name must start with a capital
      Throws /Vim(let):E729:/ g:I.interpolate('${Funcref}', {'Funcref': function('InterpolationFunc')})
    End
    It doens't throw error with invalid key in {context}
      let context = {
      \   'name': 'vital(valid)',
      \   'f': function('InterpolationFunc'),
      \   '000': 'digit',
      \   'v i m': 'space',
      \ }
      Assert Equals(g:I.interpolate("${name} is awesome", context), "vital(valid) is awesome")
    End
    It can accept a:
      Assert Equals(InterpolationFuncA('X', 'Y'), 'x is X. y is Y')
    End
  End

End

